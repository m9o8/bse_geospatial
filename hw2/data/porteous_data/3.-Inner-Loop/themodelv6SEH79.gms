$title THE model version 6SEH79
$offupper
$onuellist

*Full set of 232 markets including 3 world markets:
*Set m markets /Luanda, Cotonou, Malanville, Natitingou, Parakou, Gaborone, BoboDioulasso, Dedougou, FadaNgourma, Ouagadougou, Bujumbura, Gitega, Muyinga, Bamenda, Douala, Garoua, Yaounde, Bambari, Bangassou, Bangui, Abeche, Moundou, Ndjamena, Sarh, Brazzaville, Impfondo, PointeNoire, Abengourou, Abidjan, Bouake, Daloa, Man, Odienne, Bandundu, Bukavu, Bunia, Butembo, Gbadolite, Goma, Isiro, Kalemie, Kamina, Kananga, Kikwit, Kindu, Kinshasa, Kisangani, Kolwezi, Lubumbashi, Matadi, Mbandaka, MbujiMayi, Tshikapa, Uvira, Zongo, Djibouti, Asmara, Massawa, AddisAbaba, Awasa, BahirDar, Dessie, DireDawa, Gambela, Gode, Gondar, Jijiga, Jimma, Mekele, Nekemte, Yabelo, Libreville, Banjul, Accra, Bolgatanga, Ho, Kumasi, SekondiTakoradi, Tamale, Wa, Conakry, Kankan, Labe, Nzerekore, Bissau, Eldoret, Garissa, Kisumu, Lodwar, Mandera, Mombasa, Moyale, Nairobi, Nakuru, Wajir, Maseru, Gbarnga, Monrovia, Blantyre, Karonga, Lilongwe, Mangochi, Mzuzu, Bamako, Gao, Kayes, Mopti, Segou, Sikasso, AdelBagrou, Nouakchott, Tintane, Beira, Chimoio, Cuamba, Lichinga, Maputo, Maxixe, Milange, Nacala, Nampula, Pemba, Quelimane, Tete, XaiXai, KatimaMulilo, Oshakati, Swakopmund, Windhoek, Agadez, Arlit, Diffa, Maradi, Niamey, Tahoua, Zinder, Abuja, Akure, BeninCity, Calabar, Enugu, Gombe, Ibadan, Ilorin, Jos, Kaduna, Kano, Katsina, Lagos, Lokoja, Maiduguri, Makurdi, PortHarcourt, Sokoto, Yola, Butare, Gisenyi, Kigali, Dakar, Kaolack, SaintLouis, Tambacounda, Touba, Ziguinchor, Bo, Freetown, Kabala, Baidoa, Beledweyne, Bosaso, Galkayo, Garoowe, Hargeisa, Kismayo, Mogadishu, Bor, Juba, Malakal, Rumbek, Wau, AdDamazin, AlFashir, AlQadarif, ElGeneina, ElObeid, Kadugli, Kassala, Khartoum, Kosti, Nyala, PortSudan, Mbabane, Arusha, Bukoba, DaresSalaam, Dodoma, Iringa, Kigoma, Mbeya, Mtwara, Musoma, Mwanza, Singida, Songea, Sumbawanga, Tabora, Tanga, Kara, Lome, Arua, Gulu, Jinja, Kampala, Lira, Masindi, Mbarara, Chipata, Kabwe, Kasama, Kitwe, Livingstone, Lusaka, Mongu, Solwezi, Bulawayo, Harare, Hwange, Masvingo, Mutare, Johannesburg, Bangkok, Gulf/;
*Partial set of 50 markets including 3 world markets:
*Set m markets /Gaborone, Kalemie, Kamina, Kolwezi, Lubumbashi, Maseru, Blantyre, Karonga, Lilongwe, Mangochi, Mzuzu, Beira, Chimoio, Cuamba, Lichinga, Maputo, Maxixe, Milange, Nacala, Nampula, Pemba, Quelimane, Tete, XaiXai, KatimaMulilo, Oshakati, Swakopmund, Windhoek, Mbabane, Iringa, Mbeya, Mtwara, Songea, Sumbawanga, Chipata, Kabwe, Kasama, Kitwe, Livingstone, Lusaka, Mongu, Solwezi, Bulawayo, Harare, Hwange, Masvingo, Mutare, Johannesburg, Bangkok, Gulf/;
*SouthEast Africa: 112 markets including 3 world markets:
*Set m markets /Gaborone, Kalemie, Kamina, Kolwezi, Lubumbashi, Maseru, Blantyre, Karonga, Lilongwe, Mangochi, Mzuzu, Beira, Chimoio, Cuamba, Lichinga, Maputo, Maxixe, Milange, Nacala, Nampula, Pemba, Quelimane, Tete, XaiXai, KatimaMulilo, Oshakati, Swakopmund, Windhoek, Mbabane, Iringa, Mbeya, Mtwara, Songea, Sumbawanga, Chipata, Kabwe, Kasama, Kitwe, Livingstone, Lusaka, Mongu, Solwezi, Bulawayo, Harare, Hwange, Masvingo, Mutare, Luanda, Bujumbura, Gitega, Muyinga, Bambari, Bangassou, Bangui, Brazzaville, Impfondo, PointeNoire, Bandundu, Bukavu, Bunia, Butembo, Gbadolite, Goma, Isiro, Kananga, Kikwit, Kindu, Kinshasa, Kisangani, Matadi, Mbandaka, MbujiMayi, Tshikapa, Uvira, Zongo, Eldoret, Garissa, Kisumu, Lodwar, Mandera, Mombasa, Moyale, Nairobi, Nakuru, Wajir, Butare, Gisenyi, Kigali, Juba, Arusha, Bukoba, DaresSalaam, Dodoma, Kigoma, Musoma, Mwanza, Singida, Tabora, Tanga, Arua, Gulu, Jinja, Kampala, Lira, Masindi, Mbarara, Bor, Rumbek, Wau, Johannesburg, Gulf, Bangkok/;
*Horn of Africa: 37 markets including 1 world market;
*Set m markets /Gode, Jijiga, Yabelo, Baidoa, Beledweyne, Bosaso, Galkayo, Garoowe, Hargeisa, Kismayo, Mogadishu, Djibouti, Asmara, Massawa, AddisAbaba, Awasa, BahirDar, Dessie, DireDawa, Gambela, Gondar, Jimma, Mekele, Nekemte, Malakal, AdDamazin, AlFashir, AlQadarif, ElGeneina, ElObeid, Kadugli, Kassala, Khartoum, Kosti, Nyala, PortSudan, Gulf/;
*SEH:
Set m markets /Gaborone, Kalemie, Kamina, Kolwezi, Lubumbashi, Maseru, Blantyre, Karonga, Lilongwe, Mangochi, Mzuzu, Beira, Chimoio, Cuamba, Lichinga, Maputo, Maxixe, Milange, Nacala, Nampula, Pemba, Quelimane, Tete, XaiXai, KatimaMulilo, Oshakati, Swakopmund, Windhoek, Mbabane, Iringa, Mbeya, Mtwara, Songea, Sumbawanga, Chipata, Kabwe, Kasama, Kitwe, Livingstone, Lusaka, Mongu, Solwezi, Bulawayo, Harare, Hwange, Masvingo, Mutare, Luanda, Bujumbura, Gitega, Muyinga, Bambari, Bangassou, Bangui, Brazzaville, Impfondo, PointeNoire, Bandundu, Bukavu, Bunia, Butembo, Gbadolite, Goma, Isiro, Kananga, Kikwit, Kindu, Kinshasa, Kisangani, Matadi, Mbandaka, MbujiMayi, Tshikapa, Uvira, Zongo, Eldoret, Garissa, Kisumu, Lodwar, Mandera, Mombasa, Moyale, Nairobi, Nakuru, Wajir, Butare, Gisenyi, Kigali, Juba, Arusha, Bukoba, DaresSalaam, Dodoma, Kigoma, Musoma, Mwanza, Singida, Tabora, Tanga, Arua, Gulu, Jinja, Kampala, Lira, Masindi, Mbarara, Bor, Rumbek, Wau, Johannesburg, Gulf, Bangkok, Gode, Jijiga, Yabelo, Baidoa, Beledweyne, Bosaso, Galkayo, Garoowe, Hargeisa, Kismayo, Mogadishu, Djibouti, Asmara, Massawa, AddisAbaba, Awasa, BahirDar, Dessie, DireDawa, Gambela, Gondar, Jimma, Mekele, Nekemte, Malakal, AdDamazin, AlFashir, AlQadarif, ElGeneina, ElObeid, Kadugli, Kassala, Khartoum, Kosti, Nyala, PortSudan/;
*West Africa: 86 markets including 2 world markets;
*Set m markets /Cotonou, Malanville, Natitingou, Parakou, BoboDioulasso, Dedougou, FadaNgourma, Ouagadougou, Bamenda, Douala, Garoua, Yaounde, Abeche, Moundou, Ndjamena, Sarh, Abengourou, Abidjan, Bouake, Daloa, Man, Odienne, Libreville, Banjul, Accra, Bolgatanga, Ho, Kumasi, SekondiTakoradi, Tamale, Wa, Conakry, Kankan, Labe, Nzerekore, Bissau, Gbarnga, Monrovia, Bamako, Gao, Kayes, Mopti, Segou, Sikasso, AdelBagrou, Nouakchott, Tintane, Agadez, Arlit, Diffa, Maradi, Niamey, Tahoua, Zinder, Abuja, Akure, BeninCity, Calabar, Enugu, Gombe, Ibadan, Ilorin, Jos, Kaduna, Kano, Katsina, Lagos, Lokoja, Maiduguri, Makurdi, PortHarcourt, Sokoto, Yola, Dakar, Kaolack, SaintLouis, Tambacounda, Touba, Ziguinchor, Bo, Freetown, Kabala, Kara, Lome, Gulf, Bangkok/;
*Nigeria (greater): 40 markets including 2 world markets;
*Set m markets /Cotonou, Malanville, Parakou, Bamenda, Douala, Garoua, Yaounde, Abeche, Moundou, Ndjamena, Sarh, Libreville, Agadez, Arlit, Diffa, Maradi, Niamey, Tahoua, Zinder, Abuja, Akure, BeninCity, Calabar, Enugu, Gombe, Ibadan, Ilorin, Jos, Kaduna, Kano, Katsina, Lagos, Lokoja, Maiduguri, Makurdi, PortHarcourt, Sokoto, Yola, Gulf, Bangkok/;
*Guinea (greater): 48 markets including 2 world markets;
*Set m markets /Natitingou, BoboDioulasso, Dedougou, FadaNgourma, Ouagadougou, Abengourou, Abidjan, Bouake, Daloa, Man, Odienne, Banjul, Accra, Bolgatanga, Ho, Kumasi, SekondiTakoradi, Tamale, Wa, Conakry, Kankan, Labe, Nzerekore, Bissau, Gbarnga, Monrovia, Bamako, Gao, Kayes, Mopti, Segou, Sikasso, AdelBagrou, Nouakchott, Tintane, Dakar, Kaolack, SaintLouis, Tambacounda, Touba, Ziguinchor, Bo, Freetown, Kabala, Kara, Lome, Gulf, Bangkok/;
*WH (West and Horn): 122 markets including 2 world;
*Set m markets /Cotonou, Malanville, Natitingou, Parakou, BoboDioulasso, Dedougou, FadaNgourma, Ouagadougou, Bamenda, Douala, Garoua, Yaounde, Abeche, Moundou, Ndjamena, Sarh, Abengourou, Abidjan, Bouake, Daloa, Man, Odienne, Libreville, Banjul, Accra, Bolgatanga, Ho, Kumasi, SekondiTakoradi, Tamale, Wa, Conakry, Kankan, Labe, Nzerekore, Bissau, Gbarnga, Monrovia, Bamako, Gao, Kayes, Mopti, Segou, Sikasso, AdelBagrou, Nouakchott, Tintane, Agadez, Arlit, Diffa, Maradi, Niamey, Tahoua, Zinder, Abuja, Akure, BeninCity, Calabar, Enugu, Gombe, Ibadan, Ilorin, Jos, Kaduna, Kano, Katsina, Lagos, Lokoja, Maiduguri, Makurdi, PortHarcourt, Sokoto, Yola, Dakar, Kaolack, SaintLouis, Tambacounda, Touba, Ziguinchor, Bo, Freetown, Kabala, Kara, Lome, Gode, Jijiga, Yabelo, Baidoa, Beledweyne, Bosaso, Galkayo, Garoowe, Hargeisa, Kismayo, Mogadishu, Djibouti, Asmara, Massawa, AddisAbaba, Awasa, BahirDar, Dessie, DireDawa, Gambela, Gondar, Jimma, Mekele, Nekemte, Malakal, AdDamazin, AlFashir, AlQadarif, ElGeneina, ElObeid, Kadugli, Kassala, Khartoum, Kosti, Nyala, PortSudan, Gulf, Bangkok/;
alias(n,m);

Set c crops /Maize, Millet, Sorghum, Rice, Teff, Wheat/;
*Set c crops /Maize, Rice/;
*Set c crops /Maize, Sorghum, Rice/;
*Set c crops /Maize, Millet, Sorghum, Teff, Wheat/;
*Set c crops /Maize, Millet, Sorghum, Rice/;

alias(d,c);

Set t months /1*37/
    tfirst(t) first period
    tlast(t) last period;
tfirst(t) = yes$(ord(t) eq 1);
tlast(t) = yes$(ord(t) eq 37);

alias(tal,t);

Scalar epsi price elasticity of demand for calories;
Scalar sigma elasticity of substitution;
set scalarset /eps, sig/;
set scalarhelp /hel/;
Parameter scalarpar(scalarhelp, scalarset);
$call gdxxrw i=in_scalar.xlsx o=scalarpar.gdx par=scalarpar rng="sheet1!a1:c2" cdim=1 rdim=1
$gdxin scalarpar.gdx
$load scalarpar
epsi = scalarpar("hel","eps");
sigma = scalarpar("hel","sig");

Parameter r(m) monthly interest rate;
Parameter k(m) monthly unit storage cost;
Parameter A(m) demand shifter;
Parameter S0(m,c) initial stocks;

Parameter alpha(m,c) demand share for crop c;

Parameter Pop(m,t) population of market shed m;
Parameter H(m,c,t) harvest of crop c in market shed m in month t;

Parameter tau(m,n) bilateral trade costs;


****Importing parameter values from Excel:

set varm / r, k, A /;
set varmc /alpha/;
set varmt1 /Pop/;
set varmct /H/;
set varmn / tau /;
set vars0 / S0 /;

Parameter imp_varm(m,varm), imp_varmc(m,c), imp_vart(t), imp_varmt1(m,t), imp_varmct(m,c,t), imp_varmn(m,n), imp_varmn2(m,n), imp_vars0(m,c,vars0);

$call gdxxrw i=in_mkt.xlsx o=imp_varm.gdx par=imp_varm rng="sheet1!b1:e233" cdim=1 rdim=1
$gdxin imp_varm.gdx
$load imp_varm
r(m) = imp_varm(m,"r");
k(m) = imp_varm(m,"k");
A(m) = imp_varm(m,"A");

$call gdxxrw i=in_alpha.xlsx o=imp_varmc.gdx par=imp_varmc rng="sheet1!b1:h233" cdim=1 rdim=1
$gdxin imp_varmc.gdx
$load imp_varmc
alpha(m,c) = imp_varmc(m,c);

$call gdxxrw i=EP44c.xlsx o=imp_varmt1.gdx par=imp_varmt1 rng="EP26!a1:ec230" cdim=1 rdim=1
$gdxin imp_varmt1.gdx
$load imp_varmt1
Pop(m,t) = imp_varmt1(m,t);

$call gdxxrw i=EH44c.xlsx o=imp_varmct.gdx par=imp_varmct rng="EH26!a1:ed1375" cdim=1 rdim=2
$gdxin imp_varmct.gdx
$load imp_varmct
H(m,c,t) = imp_varmct(m,c,t);

$call gdxxrw i=in_tau2.xlsx o=imp_varmn.gdx par=imp_varmn rng="fromMatlab!b2:hz234" cdim=1 rdim=1
$gdxin imp_varmn.gdx
$load imp_varmn
tau(m,n) = imp_varmn(m,n);

$call gdxxrw i=results6A78in.xlsx o=imp_vars0.gdx par=imp_vars0 rng="s0!a1:c400" cdim=1 rdim=2
$gdxin imp_vars0.gdx
$load imp_vars0
S0(m,c) = imp_vars0(m,c,"S0");

Parameter cr indicator for crop presence in all markets;
cr(m,c)$(alpha(m,c) > 0) = 1;
Parameter crD indicator for crop presence in a non-world market;
crD(m,c)$((alpha(m,c) > 0)$(alpha(m,c)<5)) = 1;
Parameter crW indicator for crop presence in a world market;
crW(m,c)$(alpha(m,c)>5) = 1;

Parameter l all links;
l(m,n,c)$((tau(m,n) < 1000)$((tau(m,n) > 0)$((cr(m,c) = 1)$(cr(n,c)=1)))) = 1;
Parameter lD non-world links;
lD(m,n,c)$((tau(m,n) < 1000)$((tau(m,n) > 0)$((crD(m,c) = 1)$(crD(n,c)=1)))) = 1;
Parameter lW world links;
lW(m,n,c)$(l(m,n,c)$(not lD(m,n,c))) = 1;

parameter pint(m,c,t) price in world markets;
set varp / pint /;
parameter imp_pint(m,c,t);

$call gdxxrw i=in_pint.xlsx o=imp_pint.gdx par=imp_pint rng="PI79!a1:ed6" cdim=1 rdim=2
$gdxin imp_pint.gdx
$load imp_pint
pint(m,c,t) = imp_pint(m,c,t);

parameter Sin(m,c,t) storage guess from subset solution;
parameter Pin(m,c,t) price guess from subset solution;
parameter Qin(m,c,t) consumption guess from subset solution;
parameter TRin(m,n,c,t) trade guess from subset solution;

parameter imp_sin(m,c,t), imp_pin(m,c,t), imp_qin(m,c,t), imp_trin(m,n,c,t);

$call gdxxrw i=results6A78in.xlsx o=imp_pin.gdx par=imp_pin rng="p!a1:bj512" cdim=1 rdim=2
$gdxin imp_pin.gdx
$load imp_pin
$call gdxxrw i=results6A78in.xlsx o=imp_qin.gdx par=imp_qin rng="q!a1:bj512" cdim=1 rdim=2
$gdxin imp_qin.gdx
$load imp_qin
$call gdxxrw i=results6A78in.xlsx o=imp_sin.gdx par=imp_sin rng="s!a1:bi512" cdim=1 rdim=2
$gdxin imp_sin.gdx
$load imp_sin
$call gdxxrw i=results6A78in.xlsx o=imp_trin.gdx par=imp_trin rng="tr!a1:bk1000" cdim=1 rdim=3
$gdxin imp_trin.gdx
$load imp_trin

Pin(m,c,t) = imp_pin(m,c,t);
Qin(m,c,t) = imp_qin(m,c,t);
Sin(m,c,t) = imp_sin(m,c,t);
TRin(m,n,c,t) = imp_trin(m,n,c,t);

*display epsi, sigma, r, k, A, S0, alpha, bind, Pop, Ca, H, tau, cr, crD, crW, l, lD, lW, pint;
*$exit

Positive variables
      q(m,c,t) consumption of crop c in market m in month t
      p(m,c,t) price of crop c in market m in month t
      s(m,c,t) storage of crop c in market m in month t
      tr(m,n,c,t) trade of crop c from market m to market n in month t;

Equations
      quantity(m,c,t) equation of motion giving quantity (includes initial condition)
      price(m,c,t) inverse demand function
      storageNoArb(m,c,t) intertemporal arbitrage conditions
      tradeNoArb(m,n,c,t) intratemporal arbitrage conditions;

quantity(m,c,t)$(crD(m,c)) ..  H(m,c,t) + S0(m,c)$(tfirst(t)) + s(m,c,t-1)$(not tfirst(t)) - s(m,c,t) - s(m,c,t-12)$(tlast(t)) - sum(n,tr(m,n,c,t)$l(m,n,c)) + sum(n,tr(n,m,c,t)$l(n,m,c)) =e= q(m,c,t);

price(m,c,t)$(crD(m,c)) .. p(m,c,t) =e= (alpha(m,c)/q(m,c,t))*((prod(d$crD(m,d),(q(m,d,t)/alpha(m,d)$crD(m,d))**alpha(m,d)$crD(m,d)))**(1+(1/epsi)))/((A(m)*Pop(m,t))**(1/epsi));

storageNoArb(m,c,t)$((not tlast(t))$(crD(m,c))) ..  (1+r(m))*(p(m,c,t) + k(m)) =g= p(m,c,t+1);

tradeNoArb(m,n,c,t)$(l(m,n,c)) .. p(m,c,t) + tau(m,n) =g= p(n,c,t);

q.fx(m,c,t)$(not crD(m,c)) = 0;
p.fx(m,c,t)$(not cr(m,c)) = 0;
p.fx(m,c,t)$(crW(m,c)) = pint(m,c,t);
s.fx(m,c,t)$(not crD(m,c)) = 0;
s.fx(m,c,t)$(tlast(t)) = 0;
tr.fx(m,n,c,t)$(not l(m,n,c)) = 0;

tr.l(m,n,c,t)$(l(m,n,c)) = TRin(m,n,c,t);
p.l(m,c,t)$(crD(m,c)) = Pin(m,c,t);
q.l(m,c,t)$(crD(m,c)) = Qin(m,c,t);
q.lo(m,c,t)$(crD(m,c)) = 0.001;
s.l(m,c,t)$((not tlast(t))$(crD(m,c))) = Sin(m,c,t);

model africa6SEH79_MCP / quantity.p, price.q , storagenoarb.s, tradenoarb.tr/;

africa6SEH79_MCP.optfile = 1;

solve africa6SEH79_MCP using MCP ;

execute_unload "results6SEH79" s.l p.l q.l;
execute_unload "resultstr6SEH79" tr.l;
execute 'gdxxrw.exe results6SEH79.gdx o=results6SEH79.xlsx var=s.l rng=s!a2:bi520'
execute 'gdxxrw.exe results6SEH79.gdx o=results6SEH79.xlsx var=s.l rng=s0!a1'
execute 'gdxxrw.exe results6SEH79.gdx o=results6SEH79.xlsx var=p.l rng=p!a2:bj520'
execute 'gdxxrw.exe results6SEH79.gdx o=results6SEH79.xlsx var=q.l rng=q!a2:bj520'
execute 'gdxxrw.exe resultstr6SEH79.gdx o=results6SEH79.xlsx var=tr.l rng=tr!a2:bk1000'

Display s.l,tr.l,p.l,q.l;


